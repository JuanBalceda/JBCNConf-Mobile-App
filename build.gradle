buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'org.javafxports:jfxmobile-plugin:1.2.0'
    }

}

plugins {
    id "com.github.hierynomus.license" version "0.13.1"
}

license {
    header = file('LICENSE.txt')
    strictCheck true
    skipExistingHeaders true
    excludes(['**/*.json'])
}


apply plugin: 'org.javafxports.jfxmobile'

repositories {
    jcenter()
    maven {
        url 'http://nexus.gluonhq.com/nexus/content/repositories/releases'
    }
    flatDir {
        dirs 'libs'
    }
}

mainClassName = 'com.devoxx.DevoxxApplication'
preloaderClassName = 'com.devoxx.DevoxxPreloader'

// For desktop (for mobile check java.custom.properties)
applicationDefaultJvmArgs = [
        "-Djavafx.pulseLogger=false",
        "-Denable.logging=false",
        "-Dfile.encoding=UTF-8"
]

dependencies {
    compile 'com.gluonhq:glisten-afterburner:1.1.0'

    compile 'com.gluonhq:charm-glisten-connect-view:4.2.0'
    compile 'com.gluonhq:connect:1.3.0'
    compile 'com.google.zxing:core:3.2.1'
    compile 'com.gluonhq:maps:1.0.1'
    compile 'com.gluonhq:glisten-labs:1.0.0-b8'
//    compile name: 'ScenicView'
}

configurations.all {
    resolutionStrategy {
        cacheChangingModulesFor 0, 'seconds'
    }
}

jfxmobile {
    downConfig {
        version '3.1.0'
        plugins 'browser', 'cache', 'connectivity', 'dialer', 'display', 'lifecycle', 'local-notifications', 'runtime-args', 'settings', 'statusbar', 'storage'
    }
    android {
        manifest = 'src/android/AndroidManifest.xml'
        signingConfig {
            storeFile project.hasProperty('GLUON_APK_SIGN_KEYSTORE') ? file(GLUON_APK_SIGN_KEYSTORE) : null
            storePassword project.hasProperty('GLUON_APK_SIGN_STOREPASSWORD') ? GLUON_APK_SIGN_STOREPASSWORD : ''
            keyAlias project.hasProperty('GLUON_APK_SIGN_KEYALIAS') ? GLUON_APK_SIGN_KEYALIAS : ''
            keyPassword project.hasProperty('GLUON_APK_SIGN_KEYPASSWORD') ? GLUON_APK_SIGN_KEYPASSWORD : ''
        }
    }
    ios {
        launcherClassName = 'com.gluonhq.charm.down.notifications.CustomLauncher'
        infoPList = file('src/ios/Default-Info.plist')
        forceLinkClasses = [
                'com.devoxx.**.*',
                'com.gluonhq.**.*',
                'javax.xml.stream.*.',
                'io.datafx.**.*',
                'javax.annotations.**.*',
                'javax.inject.**.*',
                'javax.json.**.*',
                'org.glassfish.json.**.*'
        ]
    }
}
